
%implements GDFLIB_FilterFIR_SF_F16 "C"

%% Function: BlockTypeSetup ===================================================
%% Abstract:
%%   Declare external variables and functions
%%
%function BlockTypeSetup(block, system) void

   %if !EXISTS("GDFLIB_INCLUDED")
      %assign ::GDFLIB_INCLUDED = 1
      %<LibCacheIncludes("#include \"gdflib.h\"" + "\n")>
   %endif

   %assign ::cntFIR = 0
   %assign ::cntInitFIR = 0

%endfunction

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%%  Start Function of S-Function
%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Start(block, system) Output

   %assign ::cntInitFIR = %<::cntInitFIR> + 1

   %assign Order   = CAST("Number",Parameter[0].Value[0])
   %if ( FEVAL("mbd_s32k_version_is_newer_than", "2016"))
      %assign VectCoefBuf = SLibGetValueFromParamRec(Parameter[1], TLC_FALSE)
   %else
      %assign VectCoefBuf = GetValueFromParamRec(Parameter[1])
   %endif
   %assign VectCoefSize = LibBlockParameterSize(Parameter[1])

%assign fFIRparams = LibCreateSourceFile("Source", "Custom", "FIRparams")
%openfile tmpParamBuf
   %if !EXISTS("GDFLIB_INCLUDED_TO_FIRPARAM")
      %assign ::GDFLIB_INCLUDED_TO_FIRPARAM = 1
      #include "gdflib.h"
   %endif
    %% The 3rd input and output ports are declared in mex as a SS_INT32[256] array.
    tFrac16 InBufFIR_%<::cntInitFIR>[256];
    tFrac16 CoefBufFIR_%<::cntInitFIR>[] = {
        %foreach idx =  VectCoefSize[1]
            %if idx > 0
            , \
            %endif
            %<VectCoefBuf[idx]> \
        %endforeach
    };
    const GDFLIB_FILTERFIR_PARAM_T_F16 paramFIR_%<::cntInitFIR> = { %<Order>, &CoefBufFIR_%<::cntInitFIR>[0] };
    GDFLIB_FILTERFIR_STATE_T_F16 stateFIR_%<::cntInitFIR>;
%closefile tmpParamBuf
%<LibSetSourceFileSection(fFIRparams , "Definitions", tmpParamBuf)>

   GDFLIB_FilterFIRInit_F16 (&paramFIR_%<::cntInitFIR>, &stateFIR_%<::cntInitFIR>, &InBufFIR_%<::cntInitFIR>[0]);

%openfile tmpParamExternBuf
    extern tFrac16 InBufFIR_%<::cntInitFIR>[];
    extern tFrac16 CoefBufFIR_%<::cntInitFIR>[];
    extern const GDFLIB_FILTERFIR_PARAM_T_F16 paramFIR_%<::cntInitFIR>;
    extern GDFLIB_FILTERFIR_STATE_T_F16 stateFIR_%<::cntInitFIR>;
%closefile tmpParamExternBuf
%<LibCacheExtern(tmpParamExternBuf)>

%endfunction

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%%  Outputs Function of S-Function
%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Function: Outputs ==========================================================
%% Abstract:
%%
%function Outputs(block, system) Output

    %assign ::cntFIR = %<::cntFIR> + 1

    %assign f16In = LibBlockInputSignal(0, "", "", 0)
    %assign u16InIdx = LibBlockInputSignal(1, "", "", 0)
    %assign pf16InBuf = LibBlockInputSignalAddr(2, "", "", 0)
    %assign f16Out = LibBlockOutputSignal(0, "", "", 0)
    %assign u16OutIdx = LibBlockOutputSignal(1, "", "", 0)
    %assign pf16OutBuf = LibBlockOutputSignalAddr(2, "", "", 0)

	stateFIR_%<::cntFIR>.u16Idx = %<u16InIdx>;
    memcpy(stateFIR_%<::cntFIR>.pInBuf, %<pf16InBuf>, sizeof(tFrac16) << 8U);

    %<f16Out> = GDFLIB_FilterFIR_F16(%<f16In>, &paramFIR_%<::cntFIR>, &stateFIR_%<::cntFIR>);

	%<u16OutIdx> = stateFIR_%<::cntFIR>.u16Idx;
	memcpy(%<pf16OutBuf>, stateFIR_%<::cntFIR>.pInBuf, sizeof(tFrac16) << 8U);

%endfunction
